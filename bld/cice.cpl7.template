#! /bin/csh -f

if !(-d $CASEBUILD) mkdir $CASEBUILD
if !(-d $CASEBUILD/ciceconf) mkdir $CASEBUILD/ciceconf

#--------------------------------------------------------------------
# Invoke cice configure - output will go in $CASEBUILD/ciceconf 
#--------------------------------------------------------------------

set hgrid = "-hgrid $ICE_GRID"
if ($ICE_GRID =~ *T*) set hgrid = "-hgrid ${ICE_NX}x${ICE_NY}"

cd $CASEBUILD/ciceconf || exit -1
$CODEROOT/ice/cice/bld/configure $hgrid \
    -cice_mode $CICE_MODE \
    -nodecomp $CICE_CONFIG_OPTS || exit -1

#--------------------------------------------------------------------
# Create cice.buildnml.csh
#--------------------------------------------------------------------

# copy necessary files to $CASEROOT/Tools/Templates/
if ($NINST_ICE > 1) then
   set inst_counter = 1
   set inst_string = ""
   while ($inst_counter <= $NINST_ICE)
      set inst_string = $inst_counter
      if ($inst_counter <= 999) set inst_string = "0$inst_string"
      if ($inst_counter <=  99) set inst_string = "0$inst_string"
      if ($inst_counter <=   9) set inst_string = "0$inst_string"
      set inst_string = _${inst_string}
      if ( ! -f "$CASEROOT/user_nl_cice${inst_string}" ) then
         cp $CODEROOT/ice/cice/bld/user_nl_cice $CASEROOT/user_nl_cice${inst_string}
      endif
      @ inst_counter = $inst_counter + 1
   end 
else
   if ( ! -f "$CASEROOT/user_nl_cice" ) then
      cp $CODEROOT/ice/cice/bld/user_nl_cice $CASEROOT/user_nl_cice
   endif
endif

# create cice.buildnml.csh
cat >! $CASEBUILD/cice.buildnml.csh << EOF1
#! /bin/csh -f 

if (\$CICE_MODE == "prescribed") then
   # Error checks
   if (\$SSTICE_GRID_FILENAME == 'UNSET') then
      echo "SSTICE_GRID_FILENAME must be set for cice prescribed mode"; exit -1
   endif
   if (\$SSTICE_DATA_FILENAME == 'UNSET') then
      echo "SSTICE_DATA_FILENAME must be set for cice prescribed mode"; exit -1
   endif
   if (\$SSTICE_YEAR_ALIGN == '-999') then
      echo "SSTICE_YEAR_ALIGN must be set for cice prescribed mode"; exit -1
   endif
   if (\$SSTICE_YEAR_START == '-999') then
      echo "SSTICE_YEAR_START must be set for cice prescribed mode"; exit -1
   endif 
   if (\$SSTICE_YEAR_END == '-999') then
      echo "SSTICE_YEAR_END must be set for cice prescribed mode"; exit -1
   endif
   if (\$SSTICE_STREAM == 'CAMDATA') then    
      set stream_domxvarname = "xc"  
      set stream_domyvarname = "yc"  
   else         
      set stream_domxvarname = "lon"  
      set stream_domyvarname = "lat"  
   endif
endif

cd \$CASEBUILD/ciceconf || exit -1 
set default_ice_in_filename = "ice_in"

# Loop over ice instances
set inst_counter = 1
while (\$inst_counter <= \$NINST_ICE)

if (\$NINST_ICE > 1) then
   set inst_string = \$inst_counter
   if (\$inst_counter <= 999) set inst_string = 0\$inst_string
   if (\$inst_counter <=  99) set inst_string = 0\$inst_string
   if (\$inst_counter <=   9) set inst_string = 0\$inst_string
   set inst_string = "_\${inst_string}"    
else
   set inst_string = ""       
endif
set ice_in_filename = \${default_ice_in_filename}\${inst_string}

set ice_ic = 'default'
if (\$RUN_TYPE == startup) then
  if (\$ICE_GRID =~ *gx* && \$CICE_MODE != prescribed) then
     set ice_ic = \$DIN_LOC_ROOT/ice/cice/iced.0001-01-01.\${ICE_GRID}_20080212
  endif 
else if (\$RUN_TYPE == branch || \$RUN_TYPE == hybrid) then
  if (-e \${RUN_REFCASE}.cice\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc) then
    set ice_ic = \${RUN_REFCASE}.cice\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc
  else 
    set ice_ic = \${RUN_REFCASE}.cice.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc
  endif 
endif

cat >! \$CASEBUILD/ciceconf/cesm_namelist << EOF2
&ice_inparm
 ice_ic = '\$ice_ic'
 distribution_type = '\$CICE_DECOMPTYPE'
EOF2
if (\$CICE_MODE == "prescribed") then
cat >> \$CASEBUILD/ciceconf/cesm_namelist << EOF2
 grid_type = 'latlon' 
 grid_format = 'nc' 
 grid_file = '\$ICE_DOMAIN_PATH/\$ICE_DOMAIN_FILE' 
 kmt_file = '\$ICE_DOMAIN_PATH/\$ICE_DOMAIN_FILE' 
 model_year_align = \$SSTICE_YEAR_ALIGN 
 stream_fldfilename = '\$SSTICE_DATA_FILENAME' 
 stream_domfilename = '\$SSTICE_GRID_FILENAME' 
 stream_year_first = \$SSTICE_YEAR_START 
 stream_year_last = \$SSTICE_YEAR_END 
 stream_fldvarname ='ice_cov' 
 stream_domtvarname ='time' 
 stream_domxvarname ='\$stream_domxvarname' 
 stream_domyvarname ='\$stream_domyvarname' 
 stream_domareaname ='area' 
 stream_dommaskname ='mask' 
 stream_mapread ='NOT_SET' 
EOF2
endif
if (-e \$CASEROOT/user_nl_cice\${inst_string}) then
  \$UTILROOT/Tools/user_nl_add -user_nl_file \$CASEROOT/user_nl_cice\${inst_string} >> \$CASEBUILD/ciceconf/cesm_namelist 
endif
cat  >> \$CASEBUILD/ciceconf/cesm_namelist <<EOF2
/
EOF2

#-------------------------------------
# Call cice build-namelist - create input data list
#-------------------------------------

if (-e \$CASEBUILD/cice.input_data_list) then
  rm \$CASEBUILD/cice.input_data_list
endif

\$CODEROOT/ice/cice/bld/build-namelist -infile \$CASEBUILD/ciceconf/cesm_namelist \
    -csmdata \$DIN_LOC_ROOT  \
    -inputdata \$CASEBUILD/cice.input_data_list \
    -namelist "&cice \$CICE_NAMELIST_OPTS/" \
    -config config_cache.xml || exit -1

# Append ice_ic to cice.input_data_list
if (\$RUN_TYPE != 'branch' && \$RUN_TYPE != 'hybrid' && \${ice_ic} != 'default') then
cat >> \$CASEBUILD/cice.input_data_list << EOF2
ice_ic = \${ice_ic}
EOF2
endif

#-------------------------------------
# Copy resolved namelist to $CASEROOT/CaseDocs and $RUNDIR
#-------------------------------------

if (\$?DOCDIR) then # only set in preview_namelists utility		    
  cp \$CASEBUILD/ciceconf/ice_in \$CASEROOT/CaseDocs/\$ice_in_filename || exit -1
endif
if (-d \${RUNDIR}) then
  cp \$CASEBUILD/ciceconf/ice_in \${RUNDIR}/\$ice_in_filename || exit -2
endif

@ inst_counter = \$inst_counter + 1

end

EOF1

#--------------------------------------------------------------------
# Create cice.buildexe.csh
#--------------------------------------------------------------------

cat >! $CASEBUILD/cice.buildexe.csh << EOF1
#! /bin/csh -f 

set objdir = \$OBJROOT/ice/obj; cd \$objdir

set comp = "unknown"
if (\$COMP_INTERFACE == 'MCT' ) set comp = mct
if (\$COMP_INTERFACE == 'ESMF') set comp = esmf

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath 
EOF1

cat $CASEBUILD/ciceconf/Filepath >> $CASEBUILD/cice.buildexe.csh

cat >> $CASEBUILD/cice.buildexe.csh << EOF1
EOF

# Check for recompile if BLCKX, BLCKY or MXBLCKS changes
#-------------------------------------------------------
set recompile = FALSE
echo \${CICE_BLCKX} \${CICE_BLCKY} \${CICE_MXBLCKS} > \$objdir/iceres.new
cmp -s \$objdir/iceres.new \$objdir/iceres.old || set recompile = TRUE
if (\$recompile == 'TRUE') then
  cat \$objdir/iceres.old
  cat \$objdir/iceres.new
  echo "blckx,blcky,mxblcks has changed, removing objdir and cice, preparing for new compile"
  rm -f \$objdir/*.o
  rm -f \$objdir/*.f
  rm -f \$objdir/*.f90
endif

# Build the library
#-------------------------------------------------------
set cicedefs = "`cat \$CASEBUILD/ciceconf/CICE_cppdefs`"
set cicedefs = "\$cicedefs -DBLCKX=\$CICE_BLCKX -DBLCKY=\$CICE_BLCKY -DMXBLCKS=\$CICE_MXBLCKS"        
gmake complib -j \$GMAKE_J MODEL=cice COMPLIB=\$LIBROOT/libice.a MACFILE=\$CASEROOT/Macros.\$MACH USER_CPPDEFS="\$cicedefs" -f \$CASETOOLS/Makefile || exit 2

mv \$objdir/iceres.new \$objdir/iceres.old

wait 
exit 0
EOF1

