#! /bin/csh -f

if !(-d $CASEROOT/Buildnml_Prestage         ) mkdir $CASEROOT/Buildnml_Prestage
if !(-d $CASEROOT/Buildnml_Prestage/ciceconf) mkdir $CASEROOT/Buildnml_Prestage/ciceconf
if !(-d $CASEROOT/Buildexe                  ) mkdir $CASEROOT/Buildexe

#--------------------------------------------------------------------
# Create cice.buildnml_prestage.csh
#--------------------------------------------------------------------

if     ($CICE_MODE == 'prognostic') then
   set mode = prognostic
   set ncat = 5
else if ($CICE_MODE == 'thermo_only') then
   set mode = thermo_only
   set ncat = 1
else if ($CICE_MODE == 'data_clim_1870') then
   set mode = prescribed
   set pres_mode = clim_1870
   set ncat = 1
else if ($CICE_MODE == 'data_clim_2000') then
   set mode = prescribed
   set pres_mode = clim_2000
   set ncat = 1
endif

set ice_ic = 'default'
if ($RUN_TYPE == startup) then
  if ($ICE_GRID =~ *gx* && $mode != prescribed) then
     set ice_ic = iced.0001-01-01.${ICE_GRID}_20080212
  endif 
else if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
  set datedash  = $RUN_REFDATE-00000
  set ice_ic = $RUN_REFCASE.cice.r.$datedash
endif

cat >! $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
#! /bin/csh -f 

set exedir = \$EXEROOT/ccsm_se; cd \$exedir

set ice_ic = $ice_ic
EOF1

if ($RUN_TYPE == startup) then
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
if (\$CONTINUE_RUN != 'TRUE' && \$ice_ic != default && \$ice_ic != none) then
    set ice_ic = \$DIN_LOC_ROOT/ice/cice/\$ice_ic
endif
EOF1
endif

if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
EOF1
endif

cat >> $CASEROOT/Buildnml_Prestage/ciceconf/ccsm_namelist << EOF1
 &domain_nml
 distribution_type = 'DECOMPTYPE'
 / 
EOF1

# Determine ice grid spec needed for build-namelist

if ($ICE_GRID =~ *T*) then
   set NLON = $ICE_NX
   set NLAT = $ICE_NY
   set ice_hrgrid = ${NLON}x${NLAT}
else
   set ice_hrgrid = $ICE_GRID
endif

if ($mode == "prescribed") then  # generate config_cache.xml for prescribed mode
cat >> $CASEROOT/Buildnml_Prestage/ciceconf/config_cache.xml << EOF1
<?xml version="1.0"?>
<config_definition>
<entry id="cice_mode"     value="$mode"      ></entry>
<entry id="cice_grid"     value="$ice_hrgrid"></entry>
<entry id="cice_prestype" value="$pres_mode" ></entry>
</config_definition>
EOF1
else     # generate config_cache.xml for non-prescribed mode               
cat >> $CASEROOT/Buildnml_Prestage/ciceconf/config_cache.xml << EOF1
<?xml version="1.0"?>
<config_definition>
<entry id="cice_mode" value="$mode"    ></entry>
<entry id="cice_grid"  value="$ICE_GRID"></entry>
</config_definition>
EOF1
endif

cd $CASEROOT/Buildnml_Prestage/ciceconf || exit -1

if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
  $CODEROOT/ice/cice//bld/build-namelist -config config_cache.xml -csmdata \$DIN_LOC_ROOT -infile ccsm_namelist \
                                         -namelist "&setup_nml ice_ic='${ice_ic}' /" \
                                         -inputdata $CASEROOT/Buildnml_Prestage/cice.input_data_list || exit -1
else
  $CODEROOT/ice/cice//bld/build-namelist -config config_cache.xml -csmdata \$DIN_LOC_ROOT -infile ccsm_namelist \
					 -inputdata $CASEROOT/Buildnml_Prestage/cice.input_data_list || exit -1
endif

if ($RUN_TYPE == startup && ${ice_ic} != 'default') then
cat >> $CASEROOT/Buildnml_Prestage/cice.input_data_list << EOF1
ice_ic = \$DIN_LOC_ROOT/ice/cice/${ice_ic}
EOF1
endif
if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
if (${ice_ic} != 'default') then
cat >> $CASEROOT/Buildnml_Prestage/cice.input_data_list << EOF1
ice_ic = ${ice_ic}
EOF1
endif
endif

sed -e 's#DECOMPTYPE#\$CICE_DECOMPTYPE#' ice_in >! ice_in.tmp || exit 3
mv ice_in.tmp ice_in

cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1

cat >! ice_in << EOF
EOF1
cat ice_in >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh || exit -1
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
EOF
EOF1

#--------------------------------------------------------------------
# write out executable generating directives to resolved file
#--------------------------------------------------------------------

cat >! $CASEROOT/Buildexe/cice.buildexe.csh << EOF2
#! /bin/csh -f 

set exedir = \$EXEROOT/ccsm_se
set objdir = \$OBJROOT/ice/obj; cd \$objdir

# Filepath: List of source code directories (in order of importance).
#-----------------------------------------------------------------------
\cat >! Filepath << EOF
\$CASEROOT/SourceMods/src.cice
\$CODEROOT/ice/cice/src/drivers/ccsm_sequential
\$CODEROOT/ice/cice/src/mpi
\$CODEROOT/ice/cice/src/source
EOF
EOF2

#-----------------------------------------------------------------------

# Use simple function for ice/atm on same grid
#----------------------------------------------------------------------
cat >> $CASEROOT/Buildexe/cice.buildexe.csh << EOF2

set NLON = \$ICE_NX
set NLAT = \$ICE_NY

setenv BLCKX      \$CICE_BLCKX
setenv BLCKY      \$CICE_BLCKY
setenv MXBLCKS    \$CICE_MXBLCKS
setenv DECOMPTYPE \$CICE_DECOMPTYPE

EOF2

cat >> $CASEROOT/Buildexe/cice.buildexe.csh << EOF2

# Check for recompile if RES, ncat, BLCKX, BLCKY or MXBLCKS changes
#----------------------------------------------------------------------
set recompile = FALSE
echo \${NLON} \${NLAT} ${ncat} \${BLCKX} \${BLCKY} \${MXBLCKS} > \$objdir/iceres.new
cmp -s \$objdir/iceres.new \$objdir/iceres.old || set recompile = TRUE
if (\$recompile == 'TRUE') then
  cat \$objdir/iceres.old
  cat \$objdir/iceres.new
  echo "NLON,NLAT,ncat,blckx,blcky,mxblcks has changed, removing objdir and cice, preparing for new compile"
  rm -f \$objdir/*.o
  rm -f \$objdir/*.d
  rm -f \$objdir/*.f
  rm -f \$objdir/*.f90
  rm -f \$objdir/../*lib*
endif

# Build the library
#-----------------------------------------------------------------------
if (\$BLDTYPE == 'TRUE' || \$recompile == 'TRUE') then
   cp \$BLDROOT/mkDepends . || exit 2
   cp \$BLDROOT/mkSrcfiles . || exit 2
   set SMP = FALSE ;  if (\$NTHRDS_ICE > 1) set SMP = TRUE
   gmake complib -j \$GMAKE_J MODEL=cice COMPLIB=\$LIBROOT/libice.a SMP=\$SMP MACFILE=\$CASEROOT/Macros.\$MACH USER_CPPDEFS="-DCCSMCOUPLED -Dcoupled -Dncdf -DNXGLOB=\$NLON -DNYGLOB=\$NLAT -DNCAT=$ncat -DBLCKX=\$BLCKX -DBLCKY=\$BLCKY -DMXBLCKS=\$MXBLCKS -Dncdf" -f \$BLDROOT/Makefile || exit 2
   cp -p *comp_*.mod \$LIBROOT/include/
endif
mv \$objdir/iceres.new \$objdir/iceres.old

wait 
if !(-f \$LIBROOT/libice.a) then
  echo "ERROR: cice library not available, check SETBLD"
  exit -1
endif
exit 0
EOF2

