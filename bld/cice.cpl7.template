#! /bin/csh -fv

if !(-d $CASEROOT/Buildnml_Prestage         ) mkdir $CASEROOT/Buildnml_Prestage
if !(-d $CASEROOT/Buildnml_Prestage/ciceconf) mkdir $CASEROOT/Buildnml_Prestage/ciceconf
if !(-d $CASEROOT/Buildexe                  ) mkdir $CASEROOT/Buildexe

#--------------------------------------------------------------------
# Create cice.buildnml_prestage.csh
#--------------------------------------------------------------------

if     ($CICE_MODE == 'prognostic') then
   set mode = prognostic
   set ncat = 5
else if ($CICE_MODE == 'thermo_only') then
   set mode = thermo_only
   set ncat = 1
else if ($CICE_MODE == 'data_clim_1870') then
   set mode = prescribed
   set pres_mode = clim_1870
   set ncat = 1
else if ($CICE_MODE == 'data_clim_2000') then
   set mode = prescribed
   set pres_mode = clim_2000
   set ncat = 1
endif

set inic_file = 'default'
if ($RUN_TYPE == startup && $ICE_GRID =~ *gx*) then
  set inic_file = iced.0001-01-01.${ICE_GRID}_20080212
else if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
  set datedash  = $RUN_REFDATE-00000
  set inic_file = $RUN_REFCASE.cice.r.$datedash
endif

cat >! $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
#! /bin/csh -f 

source \$CASEROOT/env_conf         || exit -1
source \$CASEROOT/env_run          || exit -1
source \$CASEROOT/env_mach.\$MACH  || exit -1
source \$CASEROOT/env_pes.\$MACH   || exit -1

set exedir = \$EXEROOT/ccsm_se; cd \$exedir

set inic_file        = $inic_file
EOF1

if ($RUN_TYPE == startup) then
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
if (\$CONTINUE_RUN != 'TRUE' && \$inic_file != default && \$inic_file != none) then
  if (\$PRESTAGE_DATA == TRUE) then
    \$UTILROOT/Tools/ccsm_getinput ice/cice/\$inic_file   || exit 2
  else
    set inic_file = \$DIN_LOC_ROOT/ice/cice/\$inic_file
    if !(-f \$inic_file) then
      echo "local file \$inic_file is missing" ; exit 1
    endif     
  endif
endif
EOF1
endif
if ($RUN_TYPE == branch || $RUN_TYPE == hybrid) then
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
if (\$CONTINUE_RUN != 'TRUE') then
   \$UTILROOT/Tools/ccsm_getfile $RUN_REFCASE/ice/rest/\$inic_file || exit 2
endif
EOF1
endif

# Determine ice decomposition

cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1

# Get decomposition info from Configuration Wizard
setenv DECOMPTYPE spacecurve
set config = \`\$CODEROOT/../scripts/ccsm_utils/Tools/generate_cice_pop_decomp -res \$ICE_GRID -platform \$OS -model cice -nproc \$NTASKS_ICE -output all\`
if(\$config[1] >= 0) then
   setenv DECOMPTYPE \$config[6]
endif
EOF1

# Write out namelist
# &ice_nml
# inic_file = '$inic_file'
# /
cat >> $CASEROOT/Buildnml_Prestage/ciceconf/ccsm_namelist << EOF1
 &domain_nml
 distribution_type = 'DECOMPTYPE'
 / 
EOF1

# Determine ice grid spec needed for build-namelist

if ($ICE_GRID =~ *T*) then
   set NLON = $ICE_NX
   set NLAT = $ICE_NY
   set ice_hrgrid = ${NLON}x${NLAT}
else
   set ice_hrgrid = $ICE_GRID
endif

if ($mode == "prescribed") then  # generate config_cache.xml for prescribed mode
cat >> $CASEROOT/Buildnml_Prestage/ciceconf/config_cache.xml << EOF1
<?xml version="1.0"?>
<config_definition>
<entry id="cice_mode"     value="$mode"      ></entry>
<entry id="cice_grid"     value="$ice_hrgrid"></entry>
<entry id="cice_prestype" value="$pres_mode" ></entry>
</config_definition>
EOF1
else     # generate config_cache.xml for non-prescribed mode               
cat >> $CASEROOT/Buildnml_Prestage/ciceconf/config_cache.xml << EOF1
<?xml version="1.0"?>
<config_definition>
<entry id="cice_mode" value="$mode"    ></entry>
<entry id="cice_grid"  value="$ICE_GRID"></entry>
</config_definition>
EOF1
endif

cd $CASEROOT/Buildnml_Prestage/ciceconf || exit -1
$CODEROOT/ice/cice//bld/build-namelist -config config_cache.xml -csmdata \$DIN_LOC_ROOT -infile ccsm_namelist || exit -1
sed -e 's#DECOMPTYPE#\$DECOMPTYPE#' ice_in >! ice_in.tmp || exit 3
mv ice_in.tmp ice_in

cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1

cat >! ice_in << EOF
EOF1
cat ice_in >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh || exit -1
cat >> $CASEROOT/Buildnml_Prestage/cice.buildnml_prestage.csh << EOF1
EOF
EOF1

#--------------------------------------------------------------------
# write out executable generating directives to resolved file
#--------------------------------------------------------------------

cat >! $CASEROOT/Buildexe/cice.buildexe.csh << EOF2
#! /bin/csh -f 

source \$CASEROOT/env_conf || exit -1
source \$CASEROOT/env_run  || exit -1
source \$CASEROOT/env_mach.\$MACH  || exit -1
source \$CASEROOT/env_pes.\$MACH  || exit -1

set exedir = \$EXEROOT/ccsm_se
set objdir = \$OBJROOT/ice/obj; cd \$objdir

# Filepath: List of source code directories (in order of importance).
#-----------------------------------------------------------------------
\cat >! Filepath << EOF
\$CASEROOT/SourceMods/src.cice
\$CODEROOT/ice/cice/src/drivers/ccsm_sequential
\$CODEROOT/ice/cice/src/mpi
\$CODEROOT/ice/cice/src/source
EOF
EOF2

#-----------------------------------------------------------------------

# Use simple function for ice/atm on same grid
#----------------------------------------------------------------------
cat >> $CASEROOT/Buildexe/cice.buildexe.csh << EOF2

set NLON = \$ICE_NX
set NLAT = \$ICE_NY

set decomp_set  = false
set decomp_type = spacecurve

#  Get decomposition info from Configuration Wizard
# tcx temporary for bfb backwards compatability
if (\$ICE_GRID != \$ATM_GRID) then
set config = \`\$CODEROOT/../scripts/ccsm_utils/Tools/generate_cice_pop_decomp -res \$ICE_GRID -platform \$OS -model cice -nproc \$NTASKS_ICE -output all\`

if(\$config[1] >= 0) then
   set bsize_x     = \$config[3]
   set bsize_y     = \$config[4]
   set maxblocks   = \$config[5]
   set decomp_type = \$config[6]
   set decomp_set = true
endif
endif

if (\$decomp_set == false) then
  # try a 1d decomp
  @ rem = \$NLON % \$NTASKS_ICE
  if (\$rem == 0) then
    @ bsize_x = \$NLON / \$NTASKS_ICE  
    @ bsize_y = \$NLAT
    set maxblocks = 1
    set decomp_set = true 
  endif
endif

if (\$decomp_set == false) then
  # try a 2d decomp
  @ rem = \$NLON % (\$NTASKS_ICE / 2)
  if (\$rem == 0) then 
    @ bsize_x = \$NLON / (\$NTASKS_ICE / 2)  
    @ bsize_y = \$NLAT / 2
    set maxblocks = 1
    set decomp_set = true
  endif 
endif

if (\$decomp_set == false) then
  # try space filling curves
  set bsize_x   = 4
  @ rem = \$NLON % \$bsize_x
  if (\$rem != 0) set bsize_x = 2
  @ rem = \$NLON % \$bsize_x
  if (\$rem != 0) set bsize_x = 1
  set bsize_y = \$NLAT
  if (\$NTASKS_ICE > \$bsize_x) then
    @ bsize_y = \$NLAT / 2
  endif   
  #set maxblocks = 10
  # tcx how should this be computed, what should the 5 factor be?
  @ maxblocks = 5 * (( (\$NLON * \$NLAT) / (\$bsize_x * \$bsize_y * \$NTASKS_ICE)) + 1)
endif

setenv BLCKX      \$bsize_x
setenv BLCKY      \$bsize_y
setenv MXBLCKS    \$maxblocks
setenv DECOMPTYPE \$decomp_type

EOF2

cat >> $CASEROOT/Buildexe/cice.buildexe.csh << EOF2

# Check for recompile if RES, ncat, BLCKX, BLCKY or MXBLCKS changes
#----------------------------------------------------------------------
set recompile = FALSE
echo \${NLON} \${NLAT} ${ncat} \${BLCKX} \${BLCKY} \${MXBLCKS} > \$objdir/iceres.new
cmp -s \$objdir/iceres.new \$objdir/iceres.old || set recompile = TRUE
if (\$recompile == 'TRUE') then
  cat \$objdir/iceres.old
  cat \$objdir/iceres.new
  echo "NLON,NLAT,ncat,blckx,blcky,mxblcks has changed, removing objdir and cice, preparing for new compile"
  rm -f \$objdir/*.o
  rm -f \$objdir/*.d
  rm -f \$objdir/*.f
  rm -f \$objdir/*.f90
  rm -f \$objdir/../*lib*
endif

# Build the library
#-----------------------------------------------------------------------
if (\$BLDTYPE == 'TRUE' || \$recompile == 'TRUE') then
   cp \$BLDROOT/mkDepends . || exit 2
   cp \$BLDROOT/mkSrcfiles . || exit 2
   set THREAD = FALSE ;  if (\$NTHRDS_ICE > 1) set THREAD = TRUE
   gmake complib -j \$GMAKE_J VPFILE=Filepath MODEL=cice COMPLIB=\$objdir/../libcice.a \
      NXGLOB=\$NLON NYGLOB=\$NLAT NCAT=$ncat DEPINC=$INCROOT \
      BLCKX=\$BLCKX BLCKY=\$BLCKY MXBLCKS=\$MXBLCKS \
      THREAD=\$THREAD -f \$BLDROOT/Makefile MACFILE=\$CASEROOT/Macros.\$MACH || exit 2
   cp -p *comp_*.mod $LIBROOT/include/
endif
mv \$objdir/iceres.new \$objdir/iceres.old

wait 
if !(-f \$objdir/../libcice.a) then
  echo "ERROR: cice library not available, check SETBLD"
  exit -1
endif
exit 0
EOF2

